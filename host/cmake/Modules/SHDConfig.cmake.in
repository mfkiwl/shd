#
# Copyright 2014 Ettus Research LLC
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
########################################################################
#
# Find the header <shd/config.hpp> and library "libshd" for the SMINI
# Hardware Driver.  Priorty for prefix search is:
# 1) ENV(SHD_DIR)
# 2) pkg-config results, if available;
# 3) CMAKE_INSTALL_PREFIX
# 4) /usr/local/
# 5) /usr/
#
# Version info is handled by SHDConfigVersion.cmake only; not here.
#
########################################################################

# set that this file was found, for use in GNU Radio's FindSHD.cmake.
# Have to use the ENV, since this file might not allow CACHE changes.

set(ENV{SHD_CONFIG_USED} TRUE)

# set default values

SET(SHD_FOUND TRUE)
SET(SHD_RFNOC_FOUND @SHD_RFNOC_FOUND@)
SET(SHD_INCLUDE_HINTS)
SET(SHD_LIBDIR_HINTS)
SET(SHD_DIR $ENV{SHD_DIR})

IF(SHD_DIR)
    LIST(APPEND SHD_INCLUDE_HINTS ${SHD_DIR}/include)
    LIST(APPEND SHD_LIBDIR_HINTS ${SHD_DIR}/lib)
ENDIF()

INCLUDE(FindPkgConfig)
IF(PKG_CONFIG_FOUND)
  IF(NOT ${CMAKE_VERSION} VERSION_LESS "2.8.0")
    SET(SHD_QUIET "QUIET")
  ENDIF()
  PKG_CHECK_MODULES(PC_SHD ${SHD_QUIET} shd)
  IF(PC_SHD_FOUND)
    LIST(APPEND SHD_INCLUDE_HINTS ${PC_SHD_INCLUDEDIR})
    LIST(APPEND SHD_LIBDIR_HINTS ${PC_SHD_LIBDIR})
  ENDIF()
ENDIF()

LIST(APPEND SHD_INCLUDE_HINTS ${CMAKE_INSTALL_PREFIX}/include)
LIST(APPEND SHD_LIBDIR_HINTS ${CMAKE_INSTALL_PREFIX}/lib)


# Search for static libs if so required
IF( SHD_USE_STATIC_LIBS )
    SET( _SHD_ORIG_CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_FIND_LIBRARY_SUFFIXES})
    IF(WIN32)
        SET(CMAKE_FIND_LIBRARY_SUFFIXES .lib .a ${CMAKE_FIND_LIBRARY_SUFFIXES})
    ELSE()
        SET(CMAKE_FIND_LIBRARY_SUFFIXES .a )
    ENDIF()
    # This is set during build of SHD to match the installed version:
    SET(SHD_STATIC_LIB_DEPS "@SHD_LINK_LIST_STATIC@")
ENDIF()

# Verify that <shd/config.hpp> and libshd are available, and, if a
# version is provided, that SHD meets the version requirements -- no
# matter what pkg-config might think.

FIND_PATH(
    SHD_INCLUDE_DIRS
    NAMES shd/config.hpp
    HINTS ${SHD_INCLUDE_HINTS}
    PATHS /usr/local/include
          /usr/include
)

FIND_LIBRARY(
    SHD_LIBRARIES
    NAMES shd
    HINTS ${SHD_LIBDIR_HINTS}
    PATHS /usr/local/lib
          /usr/lib
)

# Set up linker flags for static linking:
IF(SHD_USE_STATIC_LIBS)
    IF(WIN32)
        MESSAGE(FATAL_ERROR "Static linking not available on Windows")
    ELSE(WIN32)
        # This works for gcc and Clang:
        SET(SHD_STATIC_LIB_LINK_FLAG "-Wl,-whole-archive ${SHD_LIBRARIES} -Wl,-no-whole-archive")
    ENDIF(WIN32)
    # Restore the library suffixes, if we changed them:
    SET(CMAKE_FIND_LIBRARY_SUFFIXES ${_SHD_ORIG_CMAKE_FIND_LIBRARY_SUFFIXES})
ENDIF(SHD_USE_STATIC_LIBS)

IF(SHD_LIBRARIES AND SHD_INCLUDE_DIRS)

  INCLUDE(FindPackageHandleStandardArgs)
  FIND_PACKAGE_HANDLE_STANDARD_ARGS(SHD DEFAULT_MSG SHD_LIBRARIES SHD_INCLUDE_DIRS)
  MARK_AS_ADVANCED(SHD_LIBRARIES SHD_INCLUDE_DIRS)

ELSEIF(SHD_FIND_REQUIRED)

  MESSAGE(FATAL_ERROR "SHD is required, but was not found.")

ENDIF()
