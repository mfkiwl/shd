#
# Copyright 2014 Ettus Research LLC
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

########################################################################
# When "find_package" is provided with SHD and a version, this file is
# called to try to determine if the requested version matches that
# provided by this SHD install.  All version checking is done herein.
########################################################################

# set that this file was found, for use in GNU Radio's FindSHD.cmake.
# Have to use the ENV, since this file might not allow CACHE changes.

set(ENV{SHD_CONFIG_VERSION_USED} TRUE)

# version values as set in cmake/Modules/SHDVersion.cmake, placed
# statically in here to avoid using Python all over again.

SET(MAJOR_VERSION @TRIMMED_VERSION_MAJOR@)
SET(API_VERSION @TRIMMED_VERSION_API@)
SET(ABI_VERSION @TRIMMED_VERSION_ABI@)
SET(PATCH_VERSION @TRIMMED_VERSION_PATCH@)
SET(DEVEL_VERSION @SHD_VERSION_DEVEL@)

SET(PACKAGE_VERSION @TRIMMED_SHD_VERSION@)
SET(ENV{SHD_PACKAGE_VERSION} ${PACKAGE_VERSION})

# There is a bug in CMake whereby calling "find_package(FOO)" within
# "find_package(FOO)" results in the version being checked in the
# second version no matter if it was set.  To get around this, check
# "PACKAGE_FIND_VERSION" and if empty set return variables to TRUE to
# make CMake happy.  Not the best solution, but it does the trick.

IF(NOT PACKAGE_FIND_VERSION)
  SET(PACKAGE_VERSION_COMPATIBLE TRUE)
  SET(PACKAGE_VERSION_EXACT TRUE)
  RETURN()
ENDIF(NOT PACKAGE_FIND_VERSION)

# Development branches of SHD don't have a patch version. This is a hack
# to add a fake patch version that should be higher than anything the user
# requests.
IF(DEVEL_VERSION)
    SET(PACKAGE_VERSION "${MAJOR_VERSION}.${API_VERSION}.${ABI_VERSION}.999")
ENDIF(DEVEL_VERSION)

# assume incorrect versioning by default
SET(PACKAGE_VERSION_COMPATIBLE FALSE)
SET(PACKAGE_VERSION_EXACT FALSE)

# do not use ABI for now
SET(SHD_USE_ABI FALSE)

# leave the ABI checking in, for now, just in case it is wanted in the
# future.  This code works nicely to find the ABI compatibility
# version from <shd/version.hpp>.
IF(SHD_USE_ABI)

  # find ABI compatible version from <shd/version.hpp>

  SET(SHD_INCLUDE_HINTS)
  SET(SHD_DIR $ENV{SHD_DIR})

  IF(SHD_DIR)
    LIST(APPEND SHD_INCLUDE_HINTS ${SHD_DIR}/include)
  ENDIF()

  INCLUDE(FindPkgConfig)
  IF(PKG_CONFIG_FOUND)
    IF(NOT ${CMAKE_VERSION} VERSION_LESS "2.8.0")
      SET(SHD_QUIET "QUIET")
    ENDIF()
    IF(PACKAGE_VERSION_EXACT)
      PKG_CHECK_MODULES(PC_SHD ${SHD_QUIET} shd=${SHD_FIND_VERSION})
    ELSE()
      PKG_CHECK_MODULES(PC_SHD ${SHD_QUIET} shd>=${SHD_FIND_VERSION})
    ENDIF()
    IF(PC_SHD_FOUND)
      LIST(APPEND SHD_INCLUDE_HINTS ${PC_SHD_INCLUDEDIR})
    ENDIF()
  ENDIF()

  LIST(APPEND SHD_INCLUDE_HINTS ${CMAKE_INSTALL_PREFIX}/include)

  # Verify that <shd/config.hpp> and libshd are available, and, if a
  # version is provided, that SHD meets the version requirements -- no
  # matter what pkg-config might think.

  FIND_PATH(
    SHD_INCLUDE_DIR
    NAMES shd/version.hpp
    HINTS ${SHD_INCLUDE_HINTS}
    PATHS /usr/local/include
          /usr/include
  )

  IF(SHD_INCLUDE_DIR)

    # extract the SHD API version from the installed header

    FILE(STRINGS "${SHD_INCLUDE_DIR}/shd/version.hpp"
      SHD_STRING_VERSION REGEX "SHD_VERSION_ABI_STRING")
    STRING(REGEX MATCH "[0-9]+\\.[0-9]+\\.[0-9]+"
      SHD_ABI_VERSION_CONCISE ${SHD_STRING_VERSION})

    # convert SHD_FIND_VERSION into concise #.#.# format for comparison

    STRING(REGEX REPLACE "([^\\.]*)\\.([^\\.]*)\\.([^\\.]*)"
      "\\1.\\2.\\3" SHD_ABI_VERSION_TMP ${SHD_ABI_VERSION_CONCISE})

    STRING(REPLACE "0" "" SHD_ABI_MAJOR ${CMAKE_MATCH_1})
    STRING(REPLACE "0" "" SHD_ABI_MINOR ${CMAKE_MATCH_2})
    STRING(REPLACE "0" "" SHD_ABI_PATCH ${CMAKE_MATCH_3})

    # fix the case where the version number is "000"

    IF(NOT SHD_ABI_MAJOR)
      SET(SHD_ABI_MAJOR "0")
    ENDIF()
    IF(NOT SHD_ABI_MINOR)
      SET(SHD_ABI_MINOR "0")
    ENDIF()
    IF(NOT SHD_ABI_PATCH)
      SET(SHD_ABI_PATCH "0")
    ENDIF()

    SET(SHD_ABI_VERSION_CONCISE ${SHD_ABI_MAJOR}.${SHD_ABI_MINOR}.${SHD_ABI_PATCH})

  ELSE(SHD_INCLUDE_DIR)

    # no header found ... not a good sign!  Assume ABI version is the
    # same as that known internally here.  Let SHDConfig.cmake fail if
    # it cannot find <shd/config.hpp> or "libshd" ...

    SET(SHD_ABI_VERSION_CONCISE ${PACKAGE_VERSION})

  ENDIF(SHD_INCLUDE_DIR)

  # check for ABI compatibility, both:
  #   ACTUAL VERSION >= DESIRED VERSION >= ABI VERSION

  IF(NOT ${PACKAGE_FIND_VERSION} VERSION_LESS ${SHD_ABI_VERSION_CONCISE} AND
     NOT ${PACKAGE_FIND_VERSION} VERSION_GREATER ${PACKAGE_VERSION})
    SET(PACKAGE_VERSION_COMPATIBLE TRUE)
  ENDIF()

ELSE(SHD_USE_ABI)

  # use API only, and assume compatible of requested <= actual
  # which is the same as "not >"

  IF(NOT ${PACKAGE_FIND_VERSION} VERSION_GREATER ${PACKAGE_VERSION})
    SET(PACKAGE_VERSION_COMPATIBLE TRUE)
  ENDIF()

ENDIF(SHD_USE_ABI)

# check for exact version

IF(${PACKAGE_FIND_VERSION} VERSION_EQUAL ${PACKAGE_VERSION})
  SET(PACKAGE_VERSION_EXACT TRUE)
ENDIF()

# Undo our patch-version-number hack
SET(PACKAGE_VERSION @TRIMMED_SHD_VERSION@)
